

(** {1 Convex Hull}

  MPRI course 2-36-1 Proof of Programs - Project 2022-2023

*)


module DummySymbols

let constant __MIN_PTS_NUM__ : int = 2

end

(** {2 Points in the Plane, Sets of Points} *)

module Point

use export DummySymbols
use export real.RealInfix
use export int.Int

(** Points in the plane are records of two real numbers *)

type pt = { x : real ; y : real }

(** Sets of points, represented as arrays *)

use export array.Array

type pt_set = array pt

(** access in or outside indexes of a set *)

predicate is_index (i:int) (s:pt_set) =
  0 <= i < s.length


(** A total order on points: (x1,y1) <= (x2,y2) if
    y1 < y2 or y1 = y2 and x1 <= x2 *)
predicate is_lower_pt (p1 p2:pt) =
  p1.y <. p2.y \/ (p1.y = p2.y /\ p1.x <=. p2.x)

(** QUESTION 1: compute the lowest-leftmost pt *)

let lowest_leftmost (s:pt_set) : int
  requires { s.length > 0 }                             (* car on accède à s[0] *)
  ensures { is_index result s }                         (* le résultat doit faire parti des choix possibles*)
  ensures { forall i:int. is_index i s -> is_lower_pt s[result] s[i] } (* il s'agit bien du plus petit *)
=
  let ref r = 0 in
  let ref rp = s[0] in
  for i = 1 to s.length - 1 do
    invariant { forall j:int. 0 <= j < i -> is_lower_pt s[r] s[j] } (* postcondition jusqu'à [i] *)
    invariant { s[r] = rp }
    invariant { is_index r s }
    let p = s[i] in
    if p.y <. rp.y || (p.y =. rp.y && p.x <=. rp.x) then
      begin
        r <- i;
        rp <- p;
      end
  done;
  r

end

(** {2 Specification of the counter-clockwise predicate} *)

module CCW

use Point

(** The ccw predicate *)

function crossproduct (p1 p2 p3:pt) : real =
  (p2.x -. p1.x) *. (p3.y -. p1.y) -. (p3.x -. p1.x) *. (p2.y -. p1.y)

predicate colinear (p1 p2 p3:pt) =
  crossproduct p1 p2 p3 = 0.0

predicate ccw (p1 p2 p3:pt) =
  crossproduct p1 p2 p3 >. 0.0

val ccw (p1 p2 p3:pt) : bool
  ensures { result <-> ccw p1 p2 p3 }

lemma knuth1:
  forall p1 p2 p3:pt. ccw p1 p2 p3 -> ccw p2 p3 p1

lemma knuth2:
  forall p1 p2 p3:pt. ccw p1 p2 p3 -> not (ccw p2 p1 p3)

lemma knuth3:
  forall p1 p2 p3:pt.
   not (colinear p1 p2 p3) -> ccw p1 p2 p3 \/ ccw p1 p3 p2

lemma knuth4:
  forall p1 p2 p3 q:pt.
    ccw q p1 p2 ->
    ccw q p2 p3 ->
    ccw q p3 p1 ->
    ccw p1 p2 p3

(* J'ai passé le [lemma cramer] en [axiom] car il n'était pas résolu
   par les prouveurs. Cela ne sert qu'à retirer le [?] dans why3ide. *)
axiom cramer:
  forall p1 p2 p3 q1 q2:pt.
    crossproduct q1 q2 p3 *. crossproduct q2 p1 p2 +.
    crossproduct q1 q2 p1 *. crossproduct q2 p2 p3 =
    crossproduct q1 q2 p2 *. crossproduct q2 p1 p3

lemma knuth5_aux:
  forall p1 p2 p3 q1 q2:pt.
    ccw q1 q2 p1 /\ ccw q2 p2 p3 /\ ccw q1 q2 p3 /\ ccw q2 p1 p2 ->
    crossproduct q1 q2 p2 *. crossproduct q2 p1 p3 >. 0.0 &&
    (crossproduct q1 q2 p2 >. 0.0 -> crossproduct q2 p1 p3 >. 0.0)

lemma knuth5:
  forall p1 p2 p3 q1 q2:pt.
    ccw q1 q2 p1 /\ ccw q1 q2 p2 /\ ccw q1 q2 p3 ->
    ccw q2 p1 p2 /\ ccw q2 p2 p3 -> ccw q2 p1 p3

(* QUESTION 2 *)

let function diff2 (k i j: int) : bool
= k <> i && k <> j

(** (all_on_left s i j) is true when all points in s, except points at
    index i and j themselves, are on the left of the vector s[i] s[j]
    *)

(* pour tout k dans len(s), s[k] est à gauche de s[i]-s[j] si le ccw est ok *)
predicate all_on_left (s:pt_set) (i j:int) =
  forall k : int.
    is_index i s ->
    is_index j s ->
    is_index k s ->
    diff2 k i j ->
    ccw s[i] s[j] s[k]

(* QUESTION 3 *)

exception Exit

let check_all_on_left (s:pt_set) (i j:int) : unit
  requires { is_index i s }                          (* acces [s[i]] *)
  requires { is_index j s }                          (* idem [j] *)
  ensures  { all_on_left s i j }                     (* sujet *)
  raises   { Exit -> not (all_on_left s i j) }       (* postcondition exceptionnelle *)
=
  for k = 0 to s.length - 1 do
    invariant { forall l: int. 0 <= l < k -> diff2 l i j -> ccw s[i] s[j] s[l] } (* tous les points passés sont bien à gauche de a-b *)
    if diff2 k i j && not ccw s[i] s[j] s[k] then raise Exit
  done

(* states that s[k] is on the right of the s[i]s[j] segment
   <-> s[j] on the left of s[i]s[k] *)
predicate is_on_right (s: pt_set) (i j k: int) =
  is_index i s ->
  is_index j s ->
  is_index k s ->
  ccw s[i] s[k] s[j]

end

(** {2 Module for Paths} *)

module Path

  use int.Int

  type path = {
    mutable size : int;
    ghost mutable elements : int -> int
  }

  predicate is_valid_index (i:int) (p:path) =
    0 <= i < p.size

  val create () : path
    ensures { result.size = 0 }

  val function nth (p: path) (i: int) : int
    requires { is_valid_index i p }
    reads    { p }
    ensures  { result = p.elements i }

  val append (p:path) (v: int) : unit
    writes  { p }
    ensures { p.size = (old p.size) + 1 }
    ensures { p.elements (old p.size) = v }
    ensures { forall i. 0 <= i < (old p.size) -> p.elements i = (old p.elements) i }

end


module ConvexHull

use Point
use CCW
use Path

predicate distinct (p:path) =
  forall i j:int.
    is_valid_index i p /\ is_valid_index j p /\ i < j ->
    p.elements i <> p.elements j

let distinct (p:path) : bool
  ensures { result <-> distinct p }
= try
    for i = 0 to p.size - 1 do
      invariant { forall k l: int. 0 <= k < l < i -> p.elements k <> p.elements l }
      for j = 0 to i - 1 do
        invariant { forall k: int. 0 <= k < j -> p.elements k <> p.elements i }
        if nth p i = nth p j then raise Exit
      done
    done;
    True
  with Exit -> False
  end

(* QUESTION 4 *)

(* - les indices du path [p] sont bien des indices de [s]
   - les éléments de [p] sont bien différents deux à deux *)
predicate wf (p: path) (s: pt_set)
= distinct p /\
  forall i: int. is_valid_index i p -> is_index (nth p i) s

let wf_path (p:path) (s:pt_set) : bool
  ensures { result <-> wf p s }
= if not distinct p then False else
  try
    for i = 0 to p.size - 1 do
      invariant { forall j: int. 0 <= j < i -> is_index (nth p j) s }
      let a = nth p i in
      if a < 0 || a >= s.length then raise Exit
    done;
    True
  with Exit -> False
  end

let function plus1mod (i len: int): int
= if i + 1 = len then 0 else i+1

predicate is_convex_hull (s:pt_set) (p:path)
= s.length >= __MIN_PTS_NUM__ /\
  wf p s /\
  (forall i: int. is_valid_index i p ->
    let j = plus1mod i p.size in
    all_on_left s (nth p i) (nth p j))

(* QUESTION 5 *)

let check_is_convex_hull (s:pt_set) (p:path) : bool
  requires { s.length >= __MIN_PTS_NUM__ }
  ensures  { result <-> is_convex_hull s p }
=
  if not wf_path p s then False else
  try
    for i = 0 to p.size - 1 do
      invariant { forall j: int. 0 <= j < i -> all_on_left s (nth p j) (nth p (plus1mod j p.size)) }
      let j = plus1mod i p.size in
      check_all_on_left s (nth p i) (nth p j)
    done;
    True
  with Exit -> False
  end

(** Properties assumed to characterize "general" set of points *)

predicate no_colinear_triple (s:pt_set) =
  forall i j k:int. is_index i s /\ is_index j s /\ is_index k s /\
    i <> j /\ i <> k /\ j <> k -> not (colinear s[i] s[j] s[k])

predicate unique_minimal_y (s:pt_set) (i:int) =
  is_index i s /\
    forall j:int. is_index j s /\ i <> j -> s[i].y <. s[j].y


(** QUESTION 7 : the largest function (see QUESTION 6 later) *)


(** extract the largest point `p` among set `s` for the relation

      R(p1,p2) := ccw pivot p1 p2

   All points are assumed to be on the left of the segment (base,pivot) *)

let function largest (s: pt_set) (pivot: int) (ghost base: pt): int
  requires { no_colinear_triple s }
  requires { length s >= __MIN_PTS_NUM__ }
  requires { is_index pivot s }
  requires {
    forall k : int. is_index k s ->
      k <> pivot ->
      s[k] <> base ->
      ccw base s[pivot] s[k] } (* all on left base-pivot *)
  ensures { is_index result s }
  ensures { result <> pivot }
  ensures { all_on_left s pivot result }
=
  let ref res = if pivot = 0 then 1 else 0 in
  for i = res+1 to s.length - 1 do
    invariant { is_index res s }
    invariant { is_index (i-1) s }
    invariant { res <> pivot } (* pour la post *)
    invariant {
      forall j: int. 0 <= j < i ->
      diff2 j pivot res ->
      ccw s[pivot] s[res] s[j] (* ceux qu'on a vu sont à gauche de notre segment pivot-res *)
    }
    if i <> pivot then
      if ccw s[pivot] s[i] s[res] then
        res <- i;
  done;
  res

(** QUESTION 6: Jarvis, not proving termination *)

function translate_left (p:pt) : pt =
  { x = p.x -. 1.0 ; y = p.y }

let jarvis_no_termination (s:pt_set) : path
  requires { no_colinear_triple s }
  requires { exists i: int. unique_minimal_y s i }
  requires { length s >= __MIN_PTS_NUM__ }
  diverges
  ensures  { is_convex_hull s result }
=
  let hd = lowest_leftmost s in
  let p = create () in
  append p hd;
  let ghost base = translate_left s[hd] in
  let next = largest s hd base in
  append p next;
  let ref oldpivot = hd in
  let ref pivot = next in
  try
    while True do
      invariant { is_index pivot s }
      invariant { is_index oldpivot s }
      invariant { p.size > 0 }
      invariant { nth p 0 <> pivot }
      invariant { nth p 0 = hd }
      invariant { nth p (p.size - 1) = pivot }
      invariant { nth p (p.size - 2) = oldpivot }
      invariant { forall i: int. 0 < i < p.size - 1 -> nth p i = pivot -> is_on_right s hd (nth p (i-1)) pivot }
      invariant { forall i: int. 1 <= i < p.size -> all_on_left s (nth p (i-1)) (nth p i) }
      invariant { forall i: int. 0 < i < p.size - 1 -> nth p i <> pivot }
      invariant { forall i j: int. is_valid_index i p -> is_valid_index j p -> i < j -> nth p i <> nth p j }
      invariant { wf p s }

      let next = largest s pivot s[oldpivot] in
      if next = hd then raise Exit;
      oldpivot <- pivot;
      pivot <- next;
      append p next;
    done;
    absurd
  with Exit -> p
  end

(** QUESTION 8: termination *)

(* A suggested ghost function *)

predicate range (f: int -> int) (n: int) (m:int) =
    forall i: int. 0 <= i < n -> 0 <= f i < m
  (** `range f n m` is true when `f` maps the domain
      `(0..n-1)` into `(0..m-1)` *)

predicate injective (f: int -> int) (n: int) =
    forall i j: int. 0 <= i < j < n -> f i <> f j
  (** `injective f n m` is true when `f` is an injection
      from `(0..n-1)` to `(0..m-1)` *)

lemma wf_injection:
  forall p: path. forall s: pt_set.
  wf p s ->
    injective (nth p) p.size /\
    range (nth p) (p.size) (s.length)

use pigeon.Pigeonhole

lemma enough_drawer_for_my_socks:
  forall p: path. forall s: pt_set.
   range (nth p) (p.size) (s.length) /\
   injective (nth p) (p.size) ->
   s.length >= p.size

let jarvis (s:pt_set) : path
  requires { no_colinear_triple s }
  requires { exists i: int. unique_minimal_y s i }
  requires { length s >= __MIN_PTS_NUM__ }
  ensures { is_convex_hull s result }
=
  let hd = lowest_leftmost s in
  let p = create () in
  append p hd;
  let ghost ref base = translate_left (s[hd]) in
  let next = largest s hd base in
  append p next;
  let ref oldpivot = hd in
  let ref pivot = next in
  try
    while True do
      (* post-condition *)
      invariant { is_index pivot s }
      invariant { is_index oldpivot s }
      invariant { p.size > 0 }
      invariant { nth p 0 <> pivot }
      invariant { nth p 0 = hd }
      invariant { nth p (p.size - 1) = pivot }
      invariant { nth p (p.size - 2) = oldpivot }
      invariant { forall i: int. 0 < i < p.size - 1 -> nth p i = pivot -> is_on_right s hd (nth p (i-1)) pivot }
      invariant { forall i: int. 1 <= i < p.size -> all_on_left s (nth p (i-1)) (nth p i) }
      invariant { forall i: int. 0 < i < p.size - 1 -> nth p i <> pivot }
      invariant { forall i j: int. is_valid_index i p -> is_valid_index j p -> i < j -> nth p i <> nth p j }
      invariant { wf p s }

      (* termination *)
      variant { s.length - p.size }

      let next = largest s pivot s[oldpivot] in
      if next = hd then raise Exit;
      oldpivot <- pivot;
      pivot <- next;
      append p next;
    done;
    absurd
  with Exit ->
    p
  end

end
